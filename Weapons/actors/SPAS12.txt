ACTOR "Spas12" : Weapon replaces SuperShotgun
{
	obituary "\ca %k \cb --> Spas12 --> \cc %o "
	inventory.pickupmessage "Spas12 found."
	
	weapon.ammotype "LoadedS12"
	weapon.ammouse 1
	weapon.ammogive 6
	
	weapon.ammotype2 "BuckShot"
	weapon.ammouse2 1
	weapon.ammogive2 6
	
	weapon.SlotNumber 3
	Decal BulletChip
	+NOALERT
	+AMMO_OPTIONAL
	+NOAUTOFIRE
	
	Scale 0.5
	
	States
	{
	Spawn:
		SPAS A -1
		Stop
		
	Select:
      S12A A 0 A_TakeInventory("IronSight",1)
	  S12A A 0 A_ZoomFactor(1,ZOOM_INSTANT)
      S12A A 0 A_Raise
      loop
	
	Ready: //1) reloading weapon swap glitch. 2) Pretty shit.
		S12A A 2 A_TakeInventory("Reloading",1)
		S12A BCDEFGHIJKLM 2 A_WeaponReady(WRF_NOPRIMARY)
		
	ReadyLoop:
		S12A M 0 A_JumpIfNoAmmo("Reload")
		S12A M 0 A_JumpIfInventory("Reloading",1,"Reload")
		S12A M 1 A_WeaponReady
		loop
		
	Deselect:
		TNT1 AAAAAAAAAAAAA 0 A_Lower
		
	DeselectLoop:
		TNT1 A 1 A_Lower
		loop
	
	Fire:
		TNT1 A 0 A_JumpIfInventory("IronSight",1,"FireIron")
		TNT1 A 0 A_JumpIfNoAmmo("DryFire")
		S12B A 0 A_PlaySoundEx("S12_Fire","SoundSlot5")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(0.1)
		S12B A 0 BRIGHT A_FireBullets (4.5,4.5,12,7,"BulletPuff") // Pump accuracy of 9, Semi accuracy of 11
		//TNT1 A 0 A_TakeInventory("loadedS12",1)
		S12B A 0 A_FireCustomMissile("SPASTracer",random(-4.5,4.5),0,0,0,0,random(-4.5,4.5))
		S12B A 0 A_FireCustomMissile("SPASTracer",random(-4.5,4.5),0,0,0,0,random(-4.5,4.5))
		S12B A 0 A_FireCustomMissile("SPASTracer",random(-4.5,4.5),0,0,0,0,random(-4.5,4.5))
		S12B A 0 A_FireCustomMissile("SPASTracer",random(-4.5,4.5),0,0,0,0,random(-4.5,4.5))
		S12B A 0 A_FireCustomMissile("SPASTracer",random(-4.5,4.5),0,0,0,0,random(-4.5,4.5))
		S12B A 0 A_FireCustomMissile("SPASTracer",random(-4.5,4.5),0,0,0,0,random(-4.5,4.5))
		S12B A 2 A_FireCustomMissile("SPASTracer",random(-4.5,4.5),0,0,0,0,random(-4.5,4.5))
	    TNT1 A 0 ACS_ExecuteAlways(505,0)
		TNT1 A 0 A_ZoomFactor(0.88)
		S12B B 2 BRIGHT A_GunFlash
		TNT1 A 0 A_ZoomFactor(1.0)
		S12B CDEFGHI 2
		S12B J 0 A_PlaySoundEx("S12_Pump","SoundSlot5")
		S12B JKLMNO 2
		S12B PPQQRRSSTTUUVVWWXXYYZZ 1 A_WeaponReady
		S12B A 0 A_JumpIfInventory("BuckShot",1,2)
		S12B A 0 A_ReFire
		Goto ReadyLoop
		S12B A 0 A_JumpIfNoAmmo(2)
		S12B A 0 A_ReFire
		goto ReadyLoop
		S12B A 0 A_GiveInventory("Reloading",1)
		S12B A 0 A_GiveInventory("ReloadAlt",1)
		goto Reload
		
	DryFire:
		S12B C 10 A_PlayWeaponSound("dryfire")
		goto ReadyLoop
		
	AltFire:
		S12C A 0 A_JumpIfInventory("IronSight",1,"AltFire2")
		S12C A 0 A_ZoomFactor(1.5)
		S12C ABCDE 1
		S12C F 0 A_GiveInventory("IronSight",1)
		S12C FGHIJKLMNO 1 A_WeaponReady(WRF_NOBOB)
		// Some kind of accuracy script
		goto IronSight
		
	AltFire2:
		S12C O 0 A_Zoomfactor(1.0)
		S12C O 0 A_TakeInventory("IronSight",1)
		S12C JIHGF 1
		S12C EDCBA 1 A_WeaponReady(WRF_NOBOB)
		goto ReadyLoop+2
		
	IronSight:
		S12C O 0 A_JumpIfInventory("Reloading",1,"Reload")
		S12C O 0 A_JumpIfNoAmmo("Reload")
		S12C O 1 A_WeaponReady(WRF_NOBOB)
		loop
		
	FireIron:
		TNT1 A 0 A_JumpIfNoAmmo("DryIron")
		TNT1 A 0 A_PlaySoundEx("S12_fire","SoundSlot5")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Recoil(0.05)
		S12D A 0 BRIGHT A_FireBullets (3,3,9,9,"BulletPuff")
		//TNT1 A 0 A_TakeInventory("loadedS12",1)
		S12D A 0 A_FireCustomMissile("SPASTracer",random(-3,3),0,0,0,0,random(-3,3))
		S12D A 0 A_FireCustomMissile("SPASTracer",random(-3,3),0,0,0,0,random(-3,3))
		S12D A 0 A_FireCustomMissile("SPASTracer",random(-3,3),0,0,0,0,random(-3,3))
		S12D A 0 A_FireCustomMissile("SPASTracer",random(-3,3),0,0,0,0,random(-3,3))
		S12D A 0 A_FireCustomMissile("SPASTracer",random(-3,3),0,0,0,0,random(-3,3))
		S12D A 0 A_FireCustomMissile("SPASTracer",random(-3,3),0,0,0,0,random(-3,3))
		S12D A 2 A_FireCustomMissile("SPASTracer",random(-3,3),0,0,0,0,random(-3,3))
		S12D A 0 A_FireCustomMissile("SPASTracer",random(-3,3),0,0,0,0,random(-3,3))
		S12D A 2 A_FireCustomMissile("SPASTracer",random(-3,3),0,0,0,0,random(-3,3))
	    TNT1 A 0 ACS_ExecuteAlways(505,0)
		TNT1 A 0 A_ZoomFactor(1.3)
		S12D B 2 BRIGHT A_GunFlash
		TNT1 A 0 A_ZoomFactor(1.5)
		S12D CDEFGHI 2
		S12D J 0 A_PlaySoundEx("S12_Pump","SoundSlot5")
		S12D JKLMNO 2
		S12D PPQQRRSSTTUUVVWWXXYY 1 A_WeaponReady
		S12D A 0 A_JumpIfInventory("BuckShot",1,2)
		S12D A 0 A_ReFire
		goto IronSight
		S12D A 0 A_JumpIfNoAmmo(2)
		S12D A 0 A_Refire
		goto IronSight
		S12D A 0 A_GiveInventory("Reloading",1)
		S12D A 0 A_GiveInventory("ReloadAlt",1)
		goto Reload
		
	DryIron:
		S12B D 10 A_PlayWeaponSound("dryfire")
		S12D A 0 A_GiveInventory("ReloadAlt",1)
		goto IronSight
		
	Reload: //This is more or less a confirm state to trigger the animation
		TNT1 A 0 A_JumpIfInventory("BuckShot",1,"ReloadAnimation")
		TNT1 A 0 A_TakeInventory("Reloading",1) // Bullets = 0. RELOAD DENIED!
		TNT1 A 0 A_JumpIfInventory("IronSight",1,"IronHack") // Fixes a visual glitch when you have 0 total ammo
		goto ReadyLoop+2
		
	IronHack: // It's so awesome, I know! :V
		goto Ironsight+2
		
	ReloadAnimation:
		S12E A 0 A_TakeInventory("IronSight",1)
		S12E A 0 A_ZoomFactor(1.0)
		S12E ABCDEFGHI 2
		goto ReloadAmmo
		
	ReloadAmmo: // at this point animation wraps up and ammo is displayed
		S12E JKLM 2 A_WeaponReady(WRF_NOFIRE)
		S12E N 0 A_GiveInventory("loadeds12",1)
		S12E N 0 A_TakeInventory("BuckShot",1)
		S12E N 2 A_PlaySoundEx("S12_R","SoundSlot5")
		S12E OPQR 2 A_WeaponReady(WRF_NOFIRE)
		S12E R 0 A_JumpIfInventory("loadeds12",6,"ReloadEnd")
		S12E R 0 A_JumpIfInventory("BuckShot",1,"ReloadAmmo")
		goto ReloadEnd
	   
	ReloadEnd:
	   S12E R 0 A_TakeInventory("Reloading",1)
	   S12E R 0 A_JumpIfInventory("ReloadAlt",1,"ReloadAlt")
	   S12E STUVWXYZ 2 A_WeaponReady(WRF_NOFIRE)
	   S12F ABCDEFG 2 A_WeaponReady(WRF_NOFIRE)
	   TNT1 A 0 A_ReFire
	   goto ReadyLoop+2
	   
	ReloadAlt: //You blew your load, you gotta reload man!
	   S12E R 0 A_TakeInventory("Reloading",1)
	   S12E R 0 A_TakeInventory("ReloadAlt",1)
	   S12E STUVWXYZ 2 A_WeaponReady(WRF_NOFIRE)
	   S12H ABC 2 A_WeaponReady(WRF_NOFIRE)
	   S12H D 0 A_PlaySoundEx("S12_PUMP","SoundSlot5")
	   S12H DEFGHIJKLMNOPQRST 2 A_WeaponReady(WRF_NOFIRE)
	   TNT1 A 0 A_ReFire
	   goto ReadyLoop+2
	}
}
