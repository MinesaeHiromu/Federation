ACTOR "Cryo Rifle" : Weapon {

  Weapon.AmmoType1 "IceAmmo"
  Weapon.AmmoUse1 10
  Weapon.AmmoGive 150
  
  Weapon.AmmoType2 "IceAmmo"
  Weapon.AmmoUse2 1

  Weapon.SelectionOrder 115
  Weapon.SlotNumber 7
  
  +NOALERT
  
  inventory.pickupmessage "Cryo Rifle found."
  obituary "\ca %k \cb --> Cryo Rifle --> \cc %o"
   
  States {
  
  Spawn:
	CRYO A -1
	Stop

  Select:
	ICEA A 0 A_TakeInventory("IronSight",1)
	ICEA A 0 A_ZoomFactor(1,ZOOM_INSTANT)
	ICEA A 0 A_Raise
	loop
	  
  Ready:  // This is for show. Just the animation
	ICEA ABCDEFGHIJKLMNO 2 A_WeaponReady(WRF_NOPRIMARY)
	  
  Readyloop:
	ICEA A 0 A_JumpIfInventory("IceAmmo",1,1)
	goto NoGas
	ICEA O 1 A_WeaponReady
	loop

  NoGas:
	ICEA O 1 A_WeaponReady
	goto Readyloop
	 
  Deselect:
	TNT1 AAAAAAAAAA 0 A_Lower
	  
  DeselectLoop: // Makes sure it wraps up
	TNT1 A 1 A_Lower
	loop
	  
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("DryFire")
	TNT1 A 0 A_Recoil(.1)
	TNT1 A 0 A_AlertMonsters
	ICEB A 2 A_FireCustomMissile("CryoBlast",random(-2,2),1,8,0,0,random(-2,2))
	TNT1 A 0 ACS_ExecuteAlways(505,0)
	ICEB BCDE 2
	TNT1 A 0 A_Refire
	ICEB FGHIJK 2 //LMNOPQRSTU
	Goto ReadyLoop
	
  AltFire:
	TNT1 A 0 A_JumpIfNoAmmo("DryFire")
	TNT1 A 0 A_Recoil(.1)
	TNT1 A 0 A_AlertMonsters
	ICEB A 1 A_FireCustomMissile("CryoBeam",0,1,8,0,0,0)
	TNT1 A 0 A_JumpIfNoAmmo("DryFire")
	ICEB B 1 A_FireCustomMissile("CryoBeam",0,1,8,0,0,0)
	TNT1 A 0 A_JumpIfNoAmmo("DryFire")
	TNT1 A 0 ACS_ExecuteAlways(505,0)
	ICEB CD 2
	ICEB E 0 A_Refire
	ICEB EFGHIJK 2
	ICEB LLMMNNOOPPQQRRSSTTUU 1 A_WeaponReady
	Goto ReadyLoop
	
  DryFire:
	ICEB A 10 A_PlayWeaponSound("dryfire")
	goto ReadyLoop
  }
}

actor CryoBlast
{
  Radius 13
  Height 8
  Speed 25
  Damage 12
  Projectile
  //+HEXENBOUNCE
  +RANDOMIZE
  RenderStyle Add
  Alpha 0.75
  scale 0.9
  SeeSound "weapons/iceshot"
  DeathSound "weapons/icehit"
  DamageType Ice
  Decal FreezeBlastMelt
  States
  {
  Spawn:
    FBLS A 6 bright A_SpawnItem("CryoTrail")
    loop
  Death:
    FBLS BCDE 4 bright
	//TNT1 A 0 A_PlaySound("misc/freeze",0)
    TNT1 AAAAA 1 BRIGHT A_SpawnItemEx("IceCrystal",0,0,0,random(-1,1),random(-1,1),random(1,2))
    stop
  }
}

actor CryoTrail
{
  Radius 13
  Height 8
  Speed 20
  Damage 0
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 0.75
  SeeSound "weapons/iceshot"
  DeathSound "weapons/icehit"
  States
  {
  Spawn:
    FRTR ABC 6 bright A_FadeOut(0.2)
    loop
  Death:
    FRTR ABC 4 bright
    stop
  }
}

actor CryoBeam
{
  Radius 2
  Height 4
  Speed 60
  Damage 3
  Projectile
  //+HEXENBOUNCE
  +RANDOMIZE
  RenderStyle Add
  Alpha 0.75
  SeeSound "weapons/iceshot"
  DeathSound "weapons/icehit"
  DamageType Ice
  Decal FreezeBlastMelt
  States
  {
  Spawn:
    ICPR NOP 1 bright
    loop
  Death:
    ICPR DEFGH 4 bright
	TNT1 A 0 A_PlaySound("misc/freeze",0)
    //TNT1 AAAAA 1 BRIGHT A_SpawnItemEx("IceCrystal",0,0,0,random(-1,1),random(-1,1),random(1,2))
    stop
  }
}

/*ACTOR IceCrystal : IceGuyFX {
  Speed 1
  Radius 8
  Height 10
  Health 1
  damage 1
  damagetype Ice
  damagefactor "Ice", 0
  PROJECTILE
  +SHOOTABLE
  -SOLID
  //+RIPPER
  -ACTIVATEIMPACT
  -ACTIVATEPCROSS
  DeathSound "IceGuyMissileExplode"
  States {
	Spawn:
		ICPR A 0
		ICPR A 0 A_PlaySound("misc/freeze",CHAN_AUTO)
		ICPR ABC 2
		ICPR A 0 A_Jump(256,"Freeze1","Freeze2","Freeze3")
	Freeze1:
		ICPR A 0 A_ChangeFlag("SOLID",1)
		ICPR A 1 A_ChangeFlag("SOLID",1)
		ICPR A 350 A_Stop
		Goto Death
	Freeze2:
		ICPR B 0 A_ChangeFlag("SOLID",1)
		ICPR B 1 A_ChangeFlag("SOLID",1)
		ICPR B 350 A_Stop
		Goto Death
	Freeze3:
		ICPR C 0 A_ChangeFlag("SOLID",1)
		ICPR C 1 A_ChangeFlag("SOLID",1)
		ICPR C 350 A_Stop
		Goto Death
	Death:
		ICPR D 4 Bright
		ICPR E 4 Bright
		ICPR FG 4 Bright
		ICPR H 3 Bright
		Stop

  }
}*/