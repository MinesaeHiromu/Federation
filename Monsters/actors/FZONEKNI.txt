ACTOR FZoneKnight : ZoneKnight 11200
{
	//$Category Federation
	Health 240
	damagefactor "KillMe", 0.0
	+NOBLOCKMONST
	+FRIENDLY
	States {
		
/******************************************************************************\
================================HEADSHOT STATES=================================
\******************************************************************************/
	
  Spawn:
	XXLF A 0                                                     				//
    XXLF A 0 A_CustomMissile("OrbitFriendly", 44, 0, 0, 2, 0)    				//
    XXLF A 0 A_CustomMissile("OrbitFriendly2", 44, 0, 180, 2, 0) 				//
    XXLF A 1 A_ChangeFlag("FRIENDLY",1)                          				// MAKE FRIENDLY
	XXLF A 0 A_CheckSight("Spawn2")                              				// DO NOT SPAWN HEADSHOT BOX 
	XXLF A 0 Thing_SetSpecial(0,226,613,0,0)         							// RGA2 EXP COMPATIBILITY
	TNT1 A 0 A_SpawnItem ("KillMe")
  SpawnLoop:
	TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 15, 40,0)          				// MAKE HEADSHOT BOX
	XXLF AB 10 A_LookEx(0,0,random(512,2048),random(1024,4096),random(150,180),"See")// WHERE ARE YOU!
	goto SpawnLoop                                                 				//
  See:
	XXLF A 0 A_CheckSight("See2")                                				//
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 15, 40,0)           				// MAKE HEADSHOT BOX
	XXLF AAAA 1 A_Chase                                             				// NORMAL CHASE
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 15, 40,0)           				// MAKE HEADSHOT BOX
	XXLF AAAA 1 A_Chase                                             				// NORMAL CHASE
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 15, 40,0)           				// MAKE HEADSHOT BOX
	XXLF BBBB 1 A_Chase                                             				// NORMAL CHASE
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 15, 40,0)           				// MAKE HEADSHOT BOX
	XXLF BBBB 1 A_Chase                                             				// NORMAL CHASE
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 15, 40,0)           				// MAKE HEADSHOT BOX
	XXLF CCCC 1 A_Chase                                             				// NORMAL CHASE
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 15, 40,0)           				// MAKE HEADSHOT BOX
	XXLF CCCC 1 A_Chase                                             				// NORMAL CHASE
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 15, 40,0)           				// MAKE HEADSHOT BOX
	XXLF DDDD 1 A_Chase                                             				// NORMAL CHASE
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 15, 40,0)           				// MAKE HEADSHOT BOX
	XXLF DDDD 1 A_Chase                                             				// NORMAL CHASE
	Loop                                                         				//

/******************************************************************************\
==============================NON-HEADSHOT STATES===============================
\******************************************************************************/

  Spawn2: 
	XXLF A 0                                                     				//
	XXLF A 0 Thing_SetSpecial(0,226,613,0,0)         							// RGA2 EXP COMPATIBILITY
  Spawn2Loop:
	XXLF AB 10 A_LookEx(0,0,random(512,2048),random(1024,4096),random(150,180),"See2")// WHERE ARE YOU!
	goto Spawn2Loop    
  See2: 
	XXLF AAAAAAAABBBBBBBBCCCCCCCCDDDDDDDD 1 A_Chase                             // NORMAL CHASE
	Loop                                                         				//

/******************************************************************************\
=================================ATTACK STATES==================================
\******************************************************************************/

  Missile:
	XXLF E 0 A_ChangeFlag("NoPain", 1)                           				// WILL NOT ENTER PAIN STATE
	XXLF E 0 A_Jump(64, "IsClose")                               				// CHANCE TEST IF CLOSE
  BulletAttack:
	XXLF E 10 A_FaceTarget                                       				//
	XXLF E 0 A_JumpIfInventory("KnightAmmo", 10, "Reload")       				// CHECK AMMO
	TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 0, 40,0)           				// MAKE HEADSHOT BOX
	XXLF F 0 bright A_CustomBulletAttack(10,10,1,random(1,3),FBulletPuff)   		// 1ST SHOT
    TNT1 A 0 A_PlaySoundEx("Infantry/pistol","SoundSlot6")
    TNT1 A 0 A_AlertMonsters
	XXLF F 2 BRIGHT A_CustomMissile("ZoneTracer",38,0,random(-10,10),2,random(-10,10))
	TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 0, 40,0)           				// MAKE HEADSHOT BOX
	XXLF E 2 A_FaceTarget                                        				//
	TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 0, 40,0)           				// MAKE HEADSHOT BOX
	XXLF F 0 bright A_CustomBulletAttack(10,10,1,random(1,3),FBulletPuff)   		// 2ND SHOT
    TNT1 A 0 A_PlaySoundEx("Infantry/pistol","SoundSlot6")
    TNT1 A 0 A_AlertMonsters
	XXLF F 2 BRIGHT A_CustomMissile("ZoneTracer",38,0,random(-10,10),2,random(-10,10))
	TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 0, 40,0)           				// MAKE HEADSHOT BOX
	XXLF E 2 A_FaceTarget                                        				//
	TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 0, 40,0)           				// MAKE HEADSHOT BOX
	XXLF F 0 bright A_CustomBulletAttack(10,10,1,random(1,3),FBulletPuff)   		// 3RD SHOT
    TNT1 A 0 A_PlaySoundEx("Infantry/pistol","SoundSlot6")
    TNT1 A 0 A_AlertMonsters
	XXLF F 2 BRIGHT A_CustomMissile("ZoneTracer",38,0,random(-10,10),2,random(-10,10))
	TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 0, 40,0)           				// MAKE HEADSHOT BOX
	XXLF E 2 A_FaceTarget                                        				//
	TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 0, 40,0)           				// MAKE HEADSHOT BOX
	XXLF F 0 bright A_CustomBulletAttack(10,10,1,random(1,3),FBulletPuff)   		// 4TH SHOT
    TNT1 A 0 A_PlaySoundEx("Infantry/pistol","SoundSlot6")
    TNT1 A 0 A_AlertMonsters
	XXLF F 2 BRIGHT A_CustomMissile("ZoneTracer",38,0,random(-10,10),2,random(-10,10))
	TNT1 A 0 A_SpawnItem ("HeadshotTarget10", 0, 40,0)           				// MAKE HEADSHOT BOX
	XXLF E 2 A_FaceTarget                                        				//
	XXLF E 0 A_GiveInventory("KnightAmmo", 1)                    				// LESSEN AMMO
	XXLF E 0 A_Jump(32, "StopShooting")                          				// CHANCE STOP SHOOTING
	XXLF E 0 A_SpidReFire                                        				// KEEP SHOOTING!
	Goto Missile+3                                               				//
  StopShooting:
	XXLF E 0                                                     				//
	goto See                                                     				//
  Reload:
	XXLF E 0 A_TakeInventory("KnightAmmo", 10)                   				// RELOAD!
	XXLF E 4 A_PlaySound("ZSec/DryFire")                         				//
	XXLF E 4 A_SpawnItem ("HeadshotTarget10", 0, 40,0)           				// MAKE HEADSHOT BOX
	XXLF E 3 A_PlaySound("ZSec/Reload")                          				//
	XXLF EEEEEEEEEEEEE 4 A_SpawnItem ("HeadshotTarget10", 0, 40,0)				// MAKE HEADSHOT BOX
	XXLF E 0 A_ChangeFlag("NoPain", 0)                           				// WILL ENTER PAIN STATE
	goto See                                                     				//
  IsClose:
	XXLF E 0 A_JumpIfCloser(1768, "Grenade")                      				// LAUNCH GRENADE IF NEAR
	Goto BulletAttack                                              				//
  Grenade:
	XXLF E 0 A_Jump(16,"SmokeGrenade")                           				// CHANCE SMOKE GRENADE
	XXLF E 8 A_FaceTarget                                        				// GRENADE!
	XXLF F 0 bright A_PlaySound("weapons/rocklf")                				//
	XXLF F 2 bright A_CustomMissile("KnightGrenadeShell",28,5,random(-15,15),2,random(0,10))//
	XXLF E 6 A_FaceTarget                                        				//
	XXLF E 0 A_ChangeFlag("NoPain", 0)                           				// WILL ENTER PAIN STATE
	Goto See                                                     				//
  SmokeGrenade:
	XXLF E 8 A_FaceTarget                                        				// SMOKE GRENADE!
	XXLF F 0 bright A_PlaySound("weapons/rocklf")                				//
	XXLF F 2 bright A_CustomMissile("KnightSmokeGrenade", 28,5,0)				//
	XXLF E 6 A_FaceTarget                                        				//
	XXLF E 0 A_ChangeFlag("NoPain", 0)                           				// WILL ENTER PAIN STATE
	Goto See                                                     				//

/******************************************************************************\
==================================PAIN STATES===================================
\******************************************************************************/

  Pain: 
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 0, 40,0)            				// MAKE HEADSHOT BOX
	XXLF G 3                                                     				//
	TNT1 A 0 A_SpawnItem ("HeadshotTarget4", 0, 40,0)            				// MAKE HEADSHOT BOX
	XXLF G 3 A_Pain                                              				// OW!
	Goto See                                                     				//
  Pain.Shock:
	XXLF G 2 A_ChangeFlag("NoPain", 1)                           				// STUNNED!
	XXLF G 300 A_Pain                                            				//
	XXLF G 0 A_ChangeFlag("NoPain", 0)                           				//
	Goto See                                                     				//

/******************************************************************************\
==================================DEATH STATES==================================
\******************************************************************************/

  Death: 
	XXLF H 0 A_JumpIfHealthLower(-60,"Splode")                   				//
	XXLF H 5 ACS_ExecuteAlways(757,0,random(26,50),0,0)          				//
    TNT1 A 0 A_AlertMonsters
	XXLF I 5 A_Scream                                            				//
	XXLF J 5 A_Fall                                              				//
	XXLF K 5                                                     				//
	XXLF L 5                                                     				//
	XXLF M 5                                                     				//
	XXLF N 0 A_Jump(2, 2)                                        				// REGENERATING!
    XXLF N 0 A_GiveInventory("isDead", 1)                        				//
	XXLF N -1                                                    				//
	stop                                                         				//
  Regenerating:
	XXLF NNNNN 70                                                				//
	XXLF NNNNN 70                                                				//
	XXLF NNNNN 70                                                				//
	XXLF N -1 Thing_Raise(0)                                     				// STILL ALIVE
	Wait                                                         				//
  Splode:
	XXLF I 1 ACS_ExecuteAlways(757,0,random(26,50),0,0)          				// xDeath
	XXLF I 0 A_SpawnDebris("NashGore_Gib9",0)                    				// GIBS!
	XXLF I 0 A_SpawnDebris("NashGore_Gib10",0)                   				//
	XXLF I 0 A_SpawnDebris("NashGore_Gib11",0)                   				//
	XXLF I 0 A_SpawnDebris("NashGore_Gib11",0)                   				//
	XXLF I 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)        				//
	XXLF I 0                                                     				//
	XXLF I 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)        				//
	XXLF I 0 A_XScream                                           				//
	XXLF I 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)        				//
	XXLF I 0 A_NoBlocking                                        				//
	XXLF I 0 A_SpawnItem("NashGore_GibGenerator",0,0,0,0)        				//
	XXLF I 0 A_GiveInventory("isDead", 1)                        				//
	XXLF W 0 A_SpawnItemEx("DEADZoneKnightHead",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)//
	stop                                                         				//
  Death.Fire2:
    TNT1 A 0 ACS_ExecuteAlways(757,0,random(26,50),0,0)          				//
    TNT1 A 0 A_AlertMonsters
    XXLF N 0 A_GiveInventory("isDead", 1)                        				//
	TNT1 A 1 A_SpawnItem("BurnDeath1", 2, 1)                     				// SCORCHED
	stop                                                         				//
  Death.head:
    TNT1 A 0 ACS_ExecuteAlways(757,0,random(26,50),0,0)          				//
	GO99 O 9 A_PlaySound("headshot/splat")                       				//
	GO99 I 0 A_SpawnDebris("NashGore_Gib3",0)                    				// GIBS!
	GO99 I 0 A_SpawnDebris("NashGore_Gib11",0)                   				//
	GO99 I 0 A_SpawnDebris("NashGore_Gib11",0)                   				//
	GO99 P 9 A_XScream                                           				//
	GO99 Q 9 A_Fall                                              				//
	GO99 R 5                                                     				//
	GO99 S 5 A_GiveInventory("isDead", 1)                        				//
	GO99 T 0 A_SpawnItemEx("DEADZoneKnight",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)// FAKE BODY
	stop                                                         				//
  Raise: 
    XXLF KJ 5 A_ChangeFlag("FRIENDLY",1)                         				// REVIVED
    XXLF N 0 A_TakeInventory("isDead", 1)                        				//
    XXLF A 0 A_CustomMissile("OrbitFriendly", 44, 0, 0, 2, 0)    				//
    XXLF A 0 A_CustomMissile("OrbitFriendly2", 44, 0, 180, 2, 0) 				//
	XXLF IH 5 Thing_SetSpecial(0,226,613,0,0)        							// RGA2 EXP COMPATIBILITY
	TNT1 A 0 A_SpawnItem ("KillMe")
	Goto See                                                     				//
	}
}

ACTOR DEADFZoneKnight
{
 +FLOORCLIP
 Radius 12
 Height 11
 States
 {
  Spawn:
   G99P T 0
   G99P T -1
   stop
 }
}

ACTOR DEADFZoneKnightHead
{
 +FLOORCLIP
 Radius 12
 Height 11
 States
 {
  Spawn:
   XXLF W 0
   XXLF W -1
   stop
 }
}
